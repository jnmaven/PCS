apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'application'
mainClassName = 'nieldw.parental.control.ref.Main'

group 'nieldw'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = 'cucumber.api.cli.Main'
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty',
                    '--glue', 'nieldw.parental.control.steps',
                    '--tags', '~@Ignore',
                    'src/test/resources/features']
        }
    }
}
cucumber.mustRunAfter test

check {
    dependsOn cucumber
}

run {
    standardInput = System.in
}

repositories {
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile group: 'info.cukes', name: 'cucumber-java', version: '1.2.4'
}
